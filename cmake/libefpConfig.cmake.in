# libefpConfig.cmake
# ------------------
#
# libefp cmake module.
# This module sets the following variables in your project::
#
#   libefp_FOUND - true if libefp and all required components found on the system
#   libefp_VERSION - libefp version in format Major.Minor.Release. Prefer target variable.
#   libefp_INCLUDE_DIRS - Directory where efp.h header is located and dependent headers. Prefer targets.
#   libefp_INCLUDE_DIR - same as DIRS. Prefer targets.
#   libefp_DEFINITIONS - Definitions to indicate libefp is present, namely USING_libefp. Prefer targets.
#   libefp_LIBRARIES - libefp library to link against plus any dependent libraries. Prefer targets.
#   libefp_LIBRARY - libefp library to link against. Prefer targets
#   libefp_FRAGLIB_DIRS - Directories (list of full paths) where EFP fragments are located. Prefer targets.
#
#
# Target variables::
#
# It is preferred to use properties set on the base target rather than using the above variables. ::
#
#   libefp_VERSION - libefp version in format Major.Minor.Release
#   libefp_FRAGLIB_DIRS - Directories (list) where EFP fragments are located. Unlike
#                         the plain variable, this target variable contains partial paths.
#
#   get_property(_ver TARGET libefp::efp PROPERTY libefp_VERSION)
#
#
# Available components::
#
#   shared - search for only shared library
#   static - search for only static library
#   deep - search for only fragment library where directory structure is layered
#   shallow - search for only fragment library where directory structure has been collapsed
#
#
# Exported targets::
#
# If libefp is found, this module defines at least the first following
# :prop_tgt:`IMPORTED` target. Target is shared _or_ static, so, for both, use
# separate, not overlapping, installations. Depending on components available,
# it may define::
#
#   libefp::efp - the main libefp library with header & defs attached.
#
#
# Suggested usage::
#
#   find_package(libefp)
#   find_package(libefp 1.5.0 EXACT CONFIG REQUIRED COMPONENTS shared)
#
#
# The following variables can be set to guide the search for this package::
#
#   libefp_DIR - CMake variable, set to directory containing this Config file
#   CMAKE_PREFIX_PATH - CMake variable, set to root directory of this package
#   PATH - environment variable, set to bin directory of this package
#   CMAKE_DISABLE_FIND_PACKAGE_libefp - CMake variable, disables
#     find_package(libefp) when not REQUIRED, perhaps to force internal build

@PACKAGE_INIT@

set(efp libefp)  # NameSpace

# check library style component
if (@BUILD_SHARED_LIBS@)  # BUILD_SHARED_LIBS
    set(${efp}_shared_FOUND 1)
else()
    set(${efp}_static_FOUND 1)
endif()

# check library language component
set(${efp}_C_FOUND 1)

# find fraglibs
if (@FRAGLIB_DEEP@)  # FRAGLIB_DEEP
    set(${efp}_deep_FOUND 1)
else()
    set(${efp}_shallow_FOUND 1)
endif()
string(REGEX REPLACE "([^;]+)" "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_DATADIR@/${efp}/\\1" ${efp}_FRAGLIB_DIRS "@FRAGLIB_DATADIRS@")  # FRAGLIB_DATADIRS


# make detectable the FindTarget*.cmake modules
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

# check library dependency available
include(CMakeFindDependencyMacro)
if(NOT TARGET tgt::lapack)
    find_dependency(TargetLAPACK)
endif()

# Check all required components are available before trying to load any
check_required_components(${efp})

#-----------------------------------------------------------------------------
# Don't include targets if this file is being picked up by another
# project which has already built this as a subproject
#-----------------------------------------------------------------------------
if(NOT TARGET ${efp}::efp)
    include("${CMAKE_CURRENT_LIST_DIR}/${efp}Targets-C.cmake")

    get_property(_loc TARGET ${efp}::efp PROPERTY LOCATION)
    get_property(_ill TARGET ${efp}::efp PROPERTY INTERFACE_LINK_LIBRARIES)
    get_property(_iid TARGET ${efp}::efp PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
    get_property(_icd TARGET ${efp}::efp PROPERTY INTERFACE_COMPILE_DEFINITIONS)
    set(${efp}_LIBRARY ${_loc})
    set(${efp}_LIBRARIES ${_loc};${_ill})
    set(${efp}_INCLUDE_DIR ${_iid})
    set(${efp}_INCLUDE_DIRS ${_iid})
    set(${efp}_DEFINITIONS ${_icd})

    if (CMAKE_VERSION VERSION_GREATER 3.15)
        message(VERBOSE "libefp::efp")

        get_property(_ver TARGET ${efp}::efp PROPERTY libefp_VERSION)
        message(VERBOSE "${efp}::efp.${efp}_VERSION   ${_ver}")
        get_property(_dir TARGET ${efp}::efp PROPERTY libefp_FRAGLIB_DIRS)
        message(VERBOSE "${efp}::efp.${efp}_FRAGLIB_DIRS ${_dir}")

        message(VERBOSE "${efp}_FOUND                  ${${efp}_FOUND}")
        message(VERBOSE "${efp}_VERSION                ${${efp}_VERSION}")
        message(VERBOSE "${efp}_DEFINITIONS            ${${efp}_DEFINITIONS}")
        message(VERBOSE "${efp}_FRAGLIB_DIRS           ${${efp}_FRAGLIB_DIRS}")

        message(VERBOSE "${efp}_LIBRARY                ${${efp}_LIBRARY}")
        message(VERBOSE "${efp}_LIBRARIES              ${${efp}_LIBRARIES}")
        message(VERBOSE "${efp}_INCLUDE_DIR            ${${efp}_INCLUDE_DIR}")
        message(VERBOSE "${efp}_INCLUDE_DIRS           ${${efp}_INCLUDE_DIRS}")
    endif()

endif()
